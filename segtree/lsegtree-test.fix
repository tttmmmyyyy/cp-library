module CPLib.LSegtree.Test;

// https://atcoder.jp/contests/language-test-202505/submissions/68392811
// https://atcoder.jp/contests/language-test-202505/submissions/68392957

import CPLib.Trait;
import CPLib.Arithmetic::pmod;
import CPLib.LSegtree;

import Random;

test : IO ();
test = (
    let rng = Random::init_by_seed(0_U64);
    let rng = *test_random(false, rng);
    let rng = *test_random(true, rng);

    test_speed;;

    pure()
);

// 文字列の結合に関するモノイド
type StrConcat = struct { v : String };
namespace StrConcat {
    make : String -> StrConcat;
    make = |v| StrConcat { v : v };
}
impl StrConcat : Monoid {
    unit = make("");
    op = |lhs, rhs| make $ lhs.@v + rhs.@v;
}
impl StrConcat : Eq {
    eq = |lhs, rhs| lhs.@v == rhs.@v;
}
impl StrConcat : ToString {
    to_string = |a| a.@v;
}

// 文字ごとにアフィン変換（mod 26）を行うモノイド
type ShiftAlphabet = struct { a : I64, b : I64 };
namespace ShiftAlphabet {
    make : I64 -> I64 -> ShiftAlphabet;
    make = |a, b| ShiftAlphabet { a : a.pmod(26), b : b.pmod(26) };
}
impl ShiftAlphabet : Monoid {
    unit = make(1, 0);
    op = |lhs, rhs| (
        let a = lhs.@a;
        let b = lhs.@b;
        let c = rhs.@a;
        let d = rhs.@b;
        // lhs(x) = ax + b, rhs(x) = cx + d,
        // x・lhs・rhs = rhs(lhs(x)) = c(ax + b) + d = cax + cb + d
        let e = (c * a).pmod(26);
        let f = (c * b + d).pmod(26);
        make(e, f)
    );
}
impl ShiftAlphabet : ToString {
    to_string = |a| "({}, {})".populate([a.@a.to_string, a.@b.to_string]);
}

// 作用の定義
impl ShiftAlphabet : Action {
    type Set ShiftAlphabet = StrConcat;
    act = |a, s| (
        let bytes = s.@v.get_bytes;
        let bytes = bytes.map(|x| 
            if x == '\0' { x }; // ヌル文字は変換しない
            let x = (x - 'a').to_I64;
            let x = (a.@a * x + a.@b).pmod(26);
            let x = x.to_U8 + 'a';
            x
        );
        let s = String::_unsafe_from_c_str(bytes);
        StrConcat::make(s)
    );
}

// 最大値モノイド
type I64Max = struct { v : I64 };
namespace I64Max {
    make : I64 -> I64Max;
    make = |v| I64Max { v : v };
}
impl I64Max : Monoid {
    unit = make(I64::minimum);
    op = |lhs, rhs| make $ lhs.@v.max(rhs.@v);
}

// 加法モノイド
type I64Add = struct { v : I64 };
namespace I64Add {
    make : I64 -> I64Add;
    make = |v| I64Add { v : v };
}
impl I64Add : Monoid {
    unit = make(0);
    op = |lhs, rhs| make $ lhs.@v + rhs.@v;
}

// 加法モノイドの最大値モノイドに対する作用
impl I64Add : Action {
    type Set I64Add = I64Max; // 作用を受ける集合の型
    act = |a, x| I64Max::make(x.@v + a.@v);
}

// ランダムな数文字のアルファベット文字列を生成する
gen_random_alphabetical_string : Random -> (Random, String);
gen_random_alphabetical_string = |rng| (
    let (rng, len) = rng.generate_I64_nonneg;
    let len = len % 6; // 0～5文字
    let (bytes, rng) = range(0, len).fold(([], rng), |_, (bytes, rng)|
        let (rng, c) = rng.generate_I64_nonneg;
        let c = (c % 26).to_U8 + 'a';
        let bytes = bytes.push_back(c);
        (bytes, rng)
    );
    let bytes = bytes.push_back('\0');
    let s = String::_unsafe_from_c_str(bytes);
    (rng, s)
);

test_random : Bool -> Random -> IO Random;
test_random = |is_build, rng| (
    // n要素のセグメント木に対してランダムな操作を行う
    let n = 100;

    let (tree, arr) = if !is_build {
        let arr = Array::fill(n, StrConcat::make(""));
        (LSegtree::init(n), arr)
    } else {
        let arr = range(0, n).map(|i| 
            let v = String::from_U8('a' + (i % 26).to_U8);
            StrConcat::make(v)
        ).to_array;
        (LSegtree::build(arr), arr)
    };

    let (_1, _2, rng) = *range(0, 1000).fold_m((tree, arr, rng), |_, (tree, arr, rng)|
        let (rng, dice) = rng.generate_I64_nonneg;
        let dice = dice % 2;
        let (tree, arr, rng) = *if dice == 0 {
            // setによる更新
            let (rng, i) = rng.generate_I64_nonneg;
            let i = i % n;
            let (rng, v) = rng.gen_random_alphabetical_string;
            let v = StrConcat::make(v);
            let tree = tree.set(i, v);
            let arr = arr.set(i, v);
            pure $ (tree, arr, rng)
        } else {
            // 範囲作用による更新
            let (rng, l) = rng.generate_I64_nonneg;
            let (rng, r) = rng.generate_I64_nonneg;
            let l = l % n;
            let r = l + r % (n - l + 1);
            let (rng, a) = rng.generate_I64_nonneg;
            let (rng, b) = rng.generate_I64_nonneg;
            let a = ShiftAlphabet::make(a, b);
            let tree = tree.act_range(l, r, a);
            let arr = range(l, r).fold(arr, |i, arr| arr.set(i, act(a, arr.@(i))));
            pure $ (tree, arr, rng)
        };

        // foldを実行
        let (rng, l) = rng.generate_I64_nonneg;
        let (rng, r) = rng.generate_I64_nonneg;
        let l = l % n;
        let r = l + r % (n - l + 1);
        let (tree, v) = tree.fold(l, r);
        let w = range(l, r).fold(Monoid::unit, |i, acc| Monoid::op(acc, arr.@(i)));
        assert_eq(|_|"lsegtree random", v, w);;

        pure $ (tree, arr, rng)
    );

    pure $ rng
);

test_speed : IO ();
test_speed = (
    // n=10^5個の要素を持つSegtreeを生成し、n回程度ランダムにクエリを投げる。
    // もしn^2実装になっていたらこのテストは終わらない。
    let n = 1e5;
    let rng = Random::init_by_seed(0_U64);
    let tree = LSegtree::init(n) : LSegtree I64Max I64Add;
    let ((tree, rng, fold_sum), time) = consumed_time_while_lazy(|_| range(0, n).fold((tree, rng, 0), |_, (tree, rng, fold_sum)| (
        // set
        let (rng, i) = rng.generate_I64_nonneg;
        let (rng, v) = rng.generate_I64_nonneg;
        let i = i % n; // 0 <= i < n
        let tree = tree.set(i, I64Max::make(v));

        // act_range
        let (rng, l) = rng.generate_I64_nonneg;
        let (rng, r) = rng.generate_I64_nonneg;
        let l = l % n; // 0 <= l < n
        let r = l + r % (n - l + 1);
        let (rng, a) = rng.generate_I64_nonneg;
        let tree = tree.act_range(l, r, I64Add::make(a));

        // fold
        let (rng, k) = rng.generate_I64_nonneg;
        let (rng, l) = rng.generate_I64_nonneg;
        let k = k % n; // 0 <= k < n
        let l = l % n; // 0 <= l < n
        let (tree, v) = tree.fold(k, l);

        let fold_sum = fold_sum.max(v.@v);

        (tree, rng, fold_sum)
    )));
    assert_eq(|_|"test_speed", fold_sum, 9223363321965027598);;
    println $ "[CPLib.LSegtree.Test] LSegtree({}): {}".populate([n.to_string, time.to_string]);;

    pure $ ()
);